/**
 * @fileOverview Hash Array Mapped Trie.
 * 
 * This code is based on: https://github.com/exclipy/pdata
 */
package(
    hash
    
    make
    
// Mutation
    beginMutation
    endMutation
    mutate
    
// Queries
    tryGet
    get
    has
    
// Single Update
    set
    modify
    remove
    
// Aggregate
    fold
    count
    pairs
    keys
    values)
{

var constant := \x -> \ -> x;

/* Configuration
 ******************************************************************************/
var SIZE := 5;

var BUCKET_SIZE := Math.pow(2, SIZE);

var mask := BUCKET_SIZE - 1;

var MAX_INDEX_NODE := BUCKET_SIZE / 2;

var MIN_ARRAY_NODE := BUCKET_SIZE / 4;

/* Nothing
 ******************************************************************************/
var nothing := ({});

var isNothing := (=== nothing);

var maybe := \val def ->
    ?isNothing val
        :def
        :val;

/* Bit Ops
 ******************************************************************************/
/**
 * Hamming weight.
 * 
 * Taken from: http://jsperf.com/hamming-weight
 */
var popcount = let
    m1 = 0x55555555,
    m2 = 0x33333333,
    m4 = 0x0f0f0f0f
in
    \x -> let
        x = x - ((x >> 1) & m1),
        x = (x & m2) + ((x >> 2) & m2),
        x = (x + (x >> 4)) & m4,
        x = x + (x >> 8),
        x = x + (x >> 16)
    in
        (x & 0x7f);

var hashFragment := \shift h ->
    (h >>> shift) & mask;

var toBitmap := (<<, 1);

var fromBitmap := \bitmap bit ->
    popcount(bitmap & (bit - 1));

/* Array Ops
 ******************************************************************************/
/**
 * Set a value in an array.
 * 
 * @param at Index to change.
 * @param v New value
 * @param arr Array.
 */
var arrayUpdate := \at v arr -> {
    var out = arr.slice(); // copy
    out.(at) = v;
    return out;
};

/**
 * Remove a value from an array .
 * 
 * @param at Index to remove.
 * @param arr Array.
 */
var arraySpliceOut := \at arr -> {
    var out = arr.slice(); // copy
    out.splice(at, 1);
    return out;
};

/**
 * Insert a value into an array.
 * 
 * @param at Index to remove.
 * @param v Value to insert,
 * @param arr Array.
 */
var arraySpliceIn := \at v arr -> {
    var out = arr.slice(); // copy
    out.splice(at, 0, v);
    return out;
};

/* 
 ******************************************************************************/
/**
 * Get 32 bit hash of string.
 * 
 * Based on:
 * http://stackoverflow.com/questions/7616461/generate-a-hash-from-string-in-javascript-jquery
 */
hash := \str -> {
    if (typeof str === 'number')
        return str;
    
    var hash = 0;
    for (var i = 0, len = str.length; i < len; i = i + 1)
    with c = str.charCodeAt(i) in {
        hash = (((hash << 5) - hash) + c) | 0;
    }
    return hash;
};

/* Node Structures
 ******************************************************************************/
/**
 * Empty node.
 */
var empty := null;

/**
 * Leaf holding a value.
 * 
 * @member edit 
 * @member hash Hash of key.
 * @member key Key.
 * @member value Value stored.
 */
var Leaf := function \edit hash key value =self-> {
    self.edit = edit;
    self.hash = hash;
    self.key = key;
    self.value = value;
};

/**
 * Leaf holding multiple values with the same hash but different keys.
 * 
 * @member edit 
 * @member hash Hash of key.
 * @member children Array of collision children node.
 */
var Collision := function \edit hash children =self-> {
    self.hash = hash;
    self.children = children;
};

/**
 * Internal node with a sparse set of children.
 * 
 * Uses a bitmap and array to pack children.
 * 
 * @member edit
 * @member mask Bitmap that encode the positions of children in the array.
 * @member children Array of child nodes.
 */
var IndexedNode := function \edit mask children =self-> {
    self.edit = edit;
    self.mask = mask;
    self.children = children;
};

/**
 * Internal node with many children.
 * 
 * @member edit
 * @member count Number of children.
 * @member children Array of child nodes.
 */
var ArrayNode := function \edit count children =self-> {
    self.edit = edit;
    self.count = count;
    self.children = children;
};

/* 
 ******************************************************************************/
var isEmpty := (!);

/**
 * Is `node` a leaf node?
 */
var isLeaf = \node ->
    (  node === empty
    || node instanceof Leaf
    || node instanceof Collision);

/**
 * Expand an indexed node into an array node.
 * 
 * @param frag Index of added child.
 * @param child Added child.
 * @param mask Index node mask before child added.
 * @param subNodes Index node children before child added.
 */
var expand = \frag child bitmap subNodes -> {
    var bit = bitmap;
    var arr = [], count = 0;
    for (var i = 0; bit; i = i + 1) {
        if (bit & 1) {
            arr.(i) = subNodes.(count);
            count = count + 1;
        }
        bit = bit >>> 1;
    }
    arr.(frag) = child;
    return new ArrayNode(0, count + 1, arr);
};

/**
 * Collapse an array node into a indexed node.
 */
var pack = \removed elements -> {
    var children = [],
        bitmap = 0;
    
    for (var i = 0, len = elements.length; i < len; i = i + 1)
    with elem = elements.(i) in {
        if (i !== removed && !isEmpty elem) {
            children.push(elem);
            bitmap = bitmap | (1 << i);
        }
    }
    return new IndexedNode(0, bitmap, children);
};

/**
 * Merge two leaf nodes.
 * 
 * @param shift Current shift.
 * @param n1 Node.
 * @param n2 Node.
 */
var mergeLeaves = \shift n1 n2 -> let
    h1 = n1.hash,
    h2 = n2.hash
in
    ?h1 === h2
        :new Collision(0, h1, [n2, n1])
        :let
            subH1 = hashFragment(shift, h1),
            subH2 = hashFragment(shift, h2)
        in
            new IndexedNode(0, toBitmap subH1 | toBitmap subH2,
                ?subH1 === subH2
                    :[mergeLeaves(shift + SIZE, n1, n2)]
                    :?subH1 < subH2 : [n1, n2] : [n2, n1]);

/**
 * Update an entry in a collision list.
 */
var updateCollisionList = \list f k ->
    ? !list.length
        :[]
        :let first = list.(0), rest = list.slice(1) in
            ?first.key === k
                :let v = f(first.value) in
                    ?isNothing v
                        :rest
                        :[v].concat(rest)
                :[first].concat(updateCollisionList(rest, f, k));

/**
 * Update an entry in a collision list.
 */
var setCollisionList = \list f k -> {
    for (var i = 0, len = list.length; i < len; ++i)
    with first = list.(i) in {
        if (first.key === k)
        with v = f(first.value) in {
            if (isNothing v) {
                list.splice(i, 1);
            } else {
                list.(i) = v;
            }
            return list;
        }
    }
};

/* Definition
 ******************************************************************************/
var Tree := function \mutable edit config root =self-> {
    self.mutable = mutable;
    self.edit = edit;
    self.config = config;
    self.root = root;
};

Tree.setRoot := \root tree -> {
    if (tree.mutable) {
        tree.root = root;
        return tree;
    }
    return new Tree(
        tree.mutable,
        tree.edit,
        tree.config,
        root);
};

/**
 * Create a new HAMT.
 * 
 * @param config#{hash, keyEq} Optional configuration of how the HAMT operates.
 */
make := let
    defKeyCompare = (===)
in
    \config ->
        new Tree(
            false,
            0, {
                keyEq: (config && config.keyCompare) || defKeyCompare,
                hash: (config && config.hash) || hash
            },
            empty);

/* Mutation
 * 
 * Mutation in HAMT uses transaction style interface. Between calls to `beginMutation`
 * and `endMuation` the tree structure and internal nodes may be modified freely.
 * However, these modifications will not effect any immutable copies captured
 * before the call to `beginMutation` or any operations after `endMuation`.
 * 
 * Mutation block allow mutation only for operations performed in that block. You
 * should generally not store the tree while it is in a mutable state.
 ******************************************************************************/
/**
 * Low level operation that marks a HAMT as mutable.
 * 
 * @param ht HAMT
 */
beginMutation := \tree ->
    new Tree(
        true,
        tree.edit + 1,
        tree.config,
        tree.root);

/**
 * Low level operation that marks a HAMT as immutable.
 * 
 * @param ht HAMT
 */
endMutation := \tree ->
     new Tree(
        false,
        tree.edit,
        tree.config,
        tree.root);

/**
 * Perform operations in `f` on a mutable version of `m`.
 * 
 * `f` is a function that is passed a reference to the mutable copy of `m`.
 * Once `f` returns, the mutable copy of `m` is locked again.
 * 
 * @param f Function taking mutable HAMT that performs operations on it.
 * @param m HAMT.
 * 
 */
mutate := \f m -> {
    var t = beginMutation(m);
    f(t);
    return endMutation(t);
};

/* Lookups
 ******************************************************************************/
var lookup;

/**
 * Leaf::get
 */
Leaf.prototype.lookup = \eq _ _ k =self->
    ?eq(self.key, k)
        :self.value
        :nothing;

/**
 * Collision::get
 */
Collision.prototype.lookup = \eq _ h k =self-> {
    if (h === self.hash) {
        for (var i = 0, len = self.children.length; i < len; i = i + 1)
        with {key value} = self.children.(i) in {
            if (eq(key, k))
                return value;
        }
    }
    return nothing;
};

/**
 * IndexedNode::get
 */
IndexedNode.prototype.lookup = \eq shift h k =self-> let
    frag = hashFragment(shift, h),
    bit = toBitmap frag
in
    ?self.mask & bit
        :lookup(eq, self.children.(fromBitmap(self.mask, bit)), shift + SIZE, h, k)
        :nothing;

/**
 * ArrayNode::get
 */
ArrayNode.prototype.lookup = \eq shift h k =self-> let
    frag = hashFragment(shift, h),
    child = self.children.(frag)
in
    lookup(eq, child, shift + SIZE, h, k);

lookup := \eq n shift h k ->
    ?isEmpty n
        :nothing
        :n.lookup(eq, shift, h, k);

/* Editing
 ******************************************************************************/
var alter;

/**
 * Leaf::modify
 */
Leaf.prototype.modify := \shift f h k =self->
    ?k === self.key
        :let v = f(self.value) in
            ?isNothing v
                :empty
                :new Leaf(0, h, k, v)
    :let v = f() in
        ?isNothing v
            :self
            :mergeLeaves(shift, self, new Leaf(0, h, k, v));

/**
 * Leaf::mutate
 */
Leaf.prototype.mutate := \edit shift f h k =self-> {
    if (k === self.key) {
        var v := f(self.value);
        if (isNothing v)
            return empty;
        
        if (edit === self.edit) {
            self.value = v;
            return self;
        } else {
            return new Leaf(edit, h, k, v);
        }
    }
    
    return let v := f() in
        ?isNothing v
            :self
            :mergeLeaves(shift, self, new Leaf(edit, h, k, v));
};

/**
 * Collision::modify
 */
Collision.prototype.modify = \shift f h k =self-> let
    list = updateCollisionList(self.children, f, k)
in
    ?list.length > 1
        :new Collision(0, self.hash, list)
        :list.(0);

/**
 * Collision::mutate
 */
Collision.prototype.mutate = \edit shift f h k =self-> {
    if (edit != self.edit) {
        var m = self.modify(shift, f, h, k);
        m.edit = edit;
        return m;
    }
    
    setCollisionList(self.children, f, k);
    if (self.children.length <= 1)
        return self.children.(0);
    return self;
};

/**
 * IndexedNode::modify
 */
IndexedNode.prototype.modify = \shift f h k =self#{children}-> let
    frag = hashFragment(shift, h),
    bit = toBitmap frag,
    indx = fromBitmap(self.mask, bit),
    exists = self.mask & bit,
    
    child = alter(false, 0, ?exists : children.(indx) : empty, shift + SIZE, f, h, k),
    
    removed = exists && isEmpty child,
    added = !exists && !isEmpty child,
    
    bitmap = ?removed
        :self.mask & ~bit
        :?added
            :self.mask | bit
            :self.mask
in
    ?!bitmap
        :empty
    
    :?removed
        :?children.length <= 2 && isLeaf children.(indx ^ 1)
            :children.(indx ^ 1) // collapse
            :new IndexedNode(
                0,
                bitmap,
                arraySpliceOut(indx, self.children))
            
    :?added
        :?self.children.length >= MAX_INDEX_NODE
            :expand(frag, child, self.mask, children)
            :new IndexedNode(
                0,
                bitmap,
                arraySpliceIn(indx, child, children))
    
    // Modify
    :new IndexedNode(
        0,
        bitmap,
        arrayUpdate(indx, child, children));

/**
 * IndexedNode::mutate
 */
IndexedNode.prototype.mutate = \edit shift f h k =self#{children}-> {
    if (edit != self.edit) {
        var m = self.modify(shift, f, h, k);
        m.edit = edit;
        return m;
    }
    
    var frag := hashFragment(shift, h),
        bit := toBitmap frag,
        indx := fromBitmap(self.mask, bit),
        exists := self.mask & bit,
        
        child := alter(true, edit, ?exists : children.(indx) : empty, shift + SIZE, f, h, k),
        
        removed := exists && isEmpty child,
        added := !exists && !isEmpty child,
        
        bitmap := ?removed
            :self.mask & ~bit
            :?added
                :self.mask | bit
                :self.mask;
    
    if (!bitmap) {
        return empty;
    } else if (removed) {
        if (children.length <= 2 && isLeaf children.(indx ^ 1)) {
            // collapse
            return children.(indx ^ 1);
        }
        self.bitmap = bitmap;
        self.children.splice(indx, 1);
    } else if (added) {
        if (self.children.length >= MAX_INDEX_NODE) {
            return expand(frag, child, self.mask, children);
        }
        self.bitmap = bitmap;
        self.children.splice(indx, 0, child);
    } else {
        // modify;
        self.bitmap = bitmap;
        self.children.(indx) = child;
    }
    return self;
};

/**
 * ArrayNode::modify
 */
ArrayNode.prototype.modify = \shift f h k =self-> let
    frag = hashFragment(shift, h),
    child = self.children.(frag),
    newChild = alter(false, 0, child, shift + SIZE, f, h, k)
in
    ?isEmpty child && !isEmpty newChild
        // add
        :new ArrayNode(
            0,
            self.count + 1,
            arrayUpdate(frag, newChild, self.children))
    
    :?!isEmpty child && isEmpty newChild
        // remove
        :?self.count - 1 <= MIN_ARRAY_NODE
            :pack(frag, self.children)
            :new ArrayNode(
                0,
                self.count - 1,
                arrayUpdate(frag, empty, self.children))
    // Modify
    :new ArrayNode(
        0,
        self.count,
        arrayUpdate(frag, newChild, self.children));

/**
 * ArrayNode::mutate
 */
ArrayNode.prototype.mutate = \edit shift f h k =self-> {
    if (edit != self.edit) {
        var m = self.modify(shift, f, h, k);
        m.edit = edit;
        return m;
    }
    
    var frag := hashFragment(shift, h),
        child = self.children.(frag),
        newChild = alter(true, edit, child, shift + SIZE, f, h, k);
    
    if (isEmpty child && !isEmpty newChild) {
        // add
        self.count = self.count + 1;
        self.children.(frag) = newChild;
    } else if (!isEmpty child && isEmpty newChild) {
        // remove
        if (self.count - 1 <= MIN_ARRAY_NODE)
            return pack(frag, self.children);
        self.count = self.count - 1;
        self.children.(frag) = empty;
    } else {
        // modify
        self.children.(frag) = newChild;
    }
    return self;
};

alter := \mutable edit n shift f h k ->
    ?isEmpty n
        :let v = f() in
            ?isNothing v
                :empty
                :new Leaf(0, h, k, v)
    :?edit
        :n.mutate(edit, shift, f, h, k)
        :n.modify(shift, f, h, k);

/* Queries
 ******************************************************************************/
/**
 * Try looking up the value stored for `k`.
 * 
 * @param alt Fallback value returned if no such key exists.
 * @param k Key.
 * @param m HAMT.
 */
tryGet := \alt k m ->
    maybe(
        lookup(
            m.config.keyEq,
            m.root,
            0,
            m.config.hash k,
            k),
        alt);

/**
 * Lookup the value stored for `k`.
 * 
 * Return null if none.
 * 
 * @param k Key.
 * @param m HAMT.
 */
get := tryGet @ null;

/**
 * Does an entry for `k` exist?
 * 
 * @param k Key.
 * @param m HAMT.
 */
has := \k m ->
    !isNothing tryGet(nothing, k, m);

/* Single Updates
 ******************************************************************************/
/**
 * Alter a value already stored in the map.
 * 
 * @param k Key.
 * @param f Function mapping current value to new value.
 * @param m HAMT
 */
modify := \k f m ->
    Tree.setRoot(
        alter(m.mutable, m.edit, m.root, 0, f, m.config.hash k, k),
        m);

/**
 * Store a value in the map.
 * 
 * @param k Key.
 * @param v Value to store.
 * @param m HAMT
 */
set := \k v m ->
    modify(k, constant v, m);

/**
 * Remove a value from the map.
 * 
 * @param k Key.
 * @param m HAMT
 */
remove := let del = constant nothing in
    \k m ->
        modify(k, del, m);


/* Fold
 ******************************************************************************/
/**
 * Leaf::fold
 */
Leaf.prototype.fold = \f z =self->
    f(z, self);

/**
 * Collision::fold
 */
Collision.prototype.fold = \f z ={children}->
    children.reduce(f, z);

/**
 * IndexedNode::fold
 * 
 * `self.children.reduce(fold@f, z)`
 */
IndexedNode.prototype.fold = \f z ={children}-> {
    var z1 = z;
    for (var i = 0, len = children.length; i < len; i = i + 1)
    with c = children.(i) in {
        z1 = ?c instanceof Leaf
            :f(z1, c)
            :c.fold(f, z1);
    } 
    return z1;
};

/**
 * ArrayNode::get
 * 
 * `self.children.reduce(fold@f, z)`
 */
ArrayNode.prototype.fold = \f z ={children}-> {
    var z1 = z;
    for (var i = 0, len = children.length; i < len; i = i + 1)
    with c = children.(i) in {
        if (c)
            z1 = ?c instanceof Leaf
                :f(z1, c)
                :c.fold(f, z1);
    } 
    return z1;
};

/**
 * Visit every entry in the map, aggregating data.
 * 
 * Order of nodes is not guaranteed.
 * 
 * @param f Function mapping previous value and key value object to new value.
 * @param z Starting value.
 * @param m Hashtrie
 */
fold := \f z {root} ->
    ?isEmpty root
        :z
        :root.fold(f, z);

/* Aggregate
 ******************************************************************************/
/**
 * Get the number of entries.
 */
count = fold @ (+, 1) @ 0;

/**
 * Get array of all key value pairs as arrays of [key, value].
 * 
 * Order is not guaranteed.
 */
pairs = let build = \p {key value} -> { p.push [key, value]; return p; } in
    \m ->
        fold(build, [], m);

/**
 * Get array of all keys.
 * 
 * Order is not guaranteed.
 */
keys = let build = \p {key} -> { p.push key; return p; } in
    \m ->
        fold(build, [], m);

/**
 * Get array of all values.
 * 
 * Order is not guaranteed, duplicates will be preserved.
 */
values = let build = \p {value} -> { p.push value; return p; } in
    \m ->
        fold(build, [], m);

}